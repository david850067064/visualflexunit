<?xml version="1.0" encoding="utf-8"?>
<!--
    Copyright (c) 2007 Allurent, Inc.
    http://code.google.com/p/visualflexunit/
    
    Permission is hereby granted, free of charge, to any person obtaining
    a copy of this software and associated documentation files (the
    "Software"), to deal in the Software without restriction, including
    without limitation the rights to use, copy, modify, merge, publish,
    distribute, sublicense, and/or sell copies of the Software, and to
    permit persons to whom the Software is furnished to do so, subject to
    the following conditions:
    
    The above copyright notice and this permission notice shall be
    included in all copies or substantial portions of the Software.
    
    THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND,
    EXPRESS OR IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF
    MERCHANTABILITY, FITNESS FOR A PARTICULAR PURPOSE AND
    NONINFRINGEMENT. IN NO EVENT SHALL THE AUTHORS OR COPYRIGHT HOLDERS BE
    LIABLE FOR ANY CLAIM, DAMAGES OR OTHER LIABILITY, WHETHER IN AN ACTION
    OF CONTRACT, TORT OR OTHERWISE, ARISING FROM, OUT OF OR IN CONNECTION
    WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE SOFTWARE.
-->

<mx:Box 
        xmlns:vfuui="com.allurent.flexunit2.flexui.*"
        xmlns:mx="http://www.adobe.com/2006/mxml">
        
    <mx:Script>
        <![CDATA[
            private static const ACTUAL_DISPLAYED:String    = "ACTUAL_DISPLAYED";
            private static const EXPECTED_DISPLAYED:String  = "EXPECTED_DISPLAYED";
        
            private var _timer:Timer;
            private var _actual:BitmapData;
            private var _expected:BitmapData;
            private var _displayState:String; 
            
            public function start(actual:BitmapData, expected:BitmapData):void
            {
                stop();
                _actual    = actual;
                _expected  = expected;
                lbl.text = "Baseline";
                img.source = new Bitmap(_expected);
                _displayState = EXPECTED_DISPLAYED;
                _timer = new Timer(1000);
                _timer.addEventListener(TimerEvent.TIMER, onTimer);
                _timer.start();
                
            }
            
            public function stop():void
            {
                if (_timer)
                {
                    _timer.stop();
                    _timer = null;
                    lbl.text = "";
                    img.source = null;
                    _displayState = null;
                }
            }
            
            private function onTimer(e:TimerEvent):void
            {
                switch (_displayState)
                {
                    case ACTUAL_DISPLAYED:
                        lbl.text = "Baseline";
                        img.source = new Bitmap(_expected);
                        _displayState = EXPECTED_DISPLAYED;
                        break;
                    case EXPECTED_DISPLAYED:
                        lbl.text = "Current";
                        img.source = new Bitmap(_actual);
                        _displayState = ACTUAL_DISPLAYED;
                        break;
                    default:
                        throw new Error("_displayState's value not covered by switch case");
                }
            }
        ]]>
    </mx:Script>

    <mx:Label id="lbl"/>
    <mx:Image id="img"/>
</mx:Box>
